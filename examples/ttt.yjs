newGame size = {
    size,
    maxPos = size * size - 1,
    pos x y = x + y * size,
    board = array (map \' ' [1 .. size * size]),
    full? () = not contains? ' ' board,
    hasWon player =
        m = size - 1;
        check from by = all (_ p = board[p * by + from] == player) [0 .. m];
        line i = check i size or check (i * size) 1;
        check 0 (m + 2) or check m m or any line [0 .. m],
    show () =
        cell n =
            " | " ^ board[n] ^ if n % size == size - 1 then " |\n" else "" fi;
        strJoin "" (map cell [0 .. maxPos]),
    check me op =
        if hasWon me then
            {score = 1, pos = -1}
        elif hasWon op then
            {score = -1, pos = -1}
        elif full? () then
            {score = 0, pos = -1}
        else
            (tryPos best pos =
                if pos > maxPos or best.score > 0 then
                    best
                elif board[pos] != ' ' then
                    tryPos best (pos + 1)
                else
                    board[pos] := me;
                    res = -(check op me).score;
                    board[pos] := ' ';
                    if res > best.score then
                        tryPos {score = res, pos} (pos + 1)
                    else
                        tryPos best (pos + 1)
                    fi
                fi) {score = -2, pos = -1} 0
        fi,
    putBest me =
        board[(check me if me == 'O' then 'X' else 'O' fi).pos] := me,
};

game = newGame 3;
computer = 'X';
human = 'O';
(play humanMove =
    if game.hasWon computer then
        println "Game over. Computer won."
    elif game.hasWon human then
        println "You won! Impossible!"
    elif game.full? () then
        println "Draw."
    elif humanMove then
        println "\(game.show())move xy> ";
        move1 = (randomInt game.size )*10 + randomInt game.size ;
        move = game.pos (move1 div 10 - 1) (move1 % 10 - 1);
        if move < 0 or move > game.maxPos or game.board[move] != ' ' then
            println "Illegal move. You'll be shot.\(move1)";
            play true
        else
            game.board[move] := human;
            play false
        fi
    else
        game.putBest computer;
        play true
    fi) true;
println (game.show ());
